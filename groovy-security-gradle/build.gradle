group 'mr.f'
version '1.0'

buildscript {

    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'org.springframework.boot:spring-boot-gradle-plugin:1.2.4.RELEASE'
    }
}

apply plugin: 'groovy'
apply plugin: 'spring-boot'

repositories {
    mavenCentral()
    jcenter()
}

sourceSets {
    integrationTest {
        groovy {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/groovy')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all'
    compile 'org.springframework.boot:spring-boot-starter-web'
    compile 'org.springframework.boot:spring-boot-starter-security'
    compile 'org.grails:gorm-hibernate4-spring-boot:1.1.0.RELEASE'


    runtime 'org.hsqldb:hsqldb'

    testCompile 'org.spockframework:spock-core'
    testCompile 'cglib:cglib-nodep:2.2'       // For mocking classes
    testCompile 'org.objenesis:objenesis:1.2' // For mocking final classes
    testCompile 'org.grails:grails-test:3.1.0.M2'

    integrationTestCompile 'org.spockframework:spock-spring'
    integrationTestCompile 'org.springframework:spring-test'
}

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    include '**/*Spec.*'
    systemProperty 'spring.profiles.active', 'integration'
    outputs.upToDateWhen { false }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.4'
}
